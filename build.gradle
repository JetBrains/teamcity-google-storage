

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.6.21"
}

ext {
    def correctVersion = project.hasProperty('versionNumber') && property('versionNumber') ==~ /\d+\.\d+\.\d+.*/
    versionNumber = correctVersion ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
    projectIds = ['group': 'teamcity-google-storage', 'version': versionNumber, 'artifact': 'google-artifact-storage']
    teamcityVersion = findProperty('teamcityVersion') ?: '2023.05'
    localRepo = findProperty('TC_LOCAL_REPO')
}

group = projectIds.group
version = projectIds.version

allprojects {
    group = projectIds.group
    version = projectIds.version

    repositories {
        if (localRepo) {
            maven {
                name = "local-teamcity-artifacts"
                url "file:///${project.getRootDir()}/${localRepo}"
            }
        }
        maven { url "https://download.jetbrains.com/teamcity-repository" }
        mavenCentral()
        google()
        mavenLocal()
    }
}

subprojects {
    apply plugin: "kotlin"

    test.useTestNG()
    jar.archiveVersion.set(null)
}